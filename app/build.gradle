apply plugin: 'io.github.rockerhieu.versionberg'
apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

versionberg {
    major 2
    minor 0
    nameTemplate '${major}.${minor}.${commitCount}.${commitSha}'
    codeTemplate '(((${major} * 100) + ${minor}) * 100) * 100000 + ${build}'
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "xyz.santeri.zippy"

        minSdkVersion 19
        targetSdkVersion 25

        versionCode versionberg.code
        versionName versionberg.name
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "ad_bottom", "\"$bannerAdId\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            versionNameSuffix ' debug'
        }
    }

    lintOptions {
        abortOnError false
        textReport true
        textOutput "stdout"
        checkAllWarnings true
        showAll true
        explainIssues true
    }

}

dependencies {
    def appcompatVersion = '25.0.0'
    def firebaseVersion = '9.6.1'
    def daggerVersion = '2.7'
    def thirtyinchVersion = '0.7.1'

    compile "com.android.support:appcompat-v7:$appcompatVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    provided 'org.glassfish:javax.annotation:10.0-b28' // Required by Dagger

    compile "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinchVersion"
    compile "net.grandcentrix.thirtyinch:thirtyinch-rx:$thirtyinchVersion"

    compile 'com.google.firebase:firebase-core:9.6.1'
    compile "com.google.firebase:firebase-ads:$firebaseVersion"

    compile 'io.reactivex:rxjava:1.2.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    compile 'com.jakewharton.timber:timber:4.3.1'

    compile 'org.jsoup:jsoup:1.9.2'

    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.github.jksiezni.permissive:permissive:0.1'
}

apply plugin: 'com.google.gms.google-services'

kapt {
    generateStubs = true
}
